
(** {1 Two-Player Games

  MPRI course 2-36-1 Proof of Programs - Project 2023-2024

*)


(** {2 The "Min" Game} *)

module MinGame

use export int.Int
use export array.Array
use export option.Option


(** type for players *)
type player = Alice | Bob

(** type for configurations of the game *)
type config = {
  stack : array int; (* stack of numbers, e.g. [ 20 ; 17 ; 24 ; -30 ; 27 ] *)
  pos : int;         (* position in the stack, i.e. index of the first number
                        to be picked *)
  turn : player;     (* player who will play next move *)
  score_alice : int; (* current score of Alice *)
  score_bob : int;   (* current score of Bob *)
}

(** initial configuration from a given stack of numbers *)
function initial_config (stack: array int) : config =
  { stack = stack; pos = 0; turn = Alice; score_alice = 0; score_bob = 0 }

(** QUESTION 1 *)

(** invariant on configurations *)
predicate inv(c:config) =
  0 <= c.pos /\ c.pos <= c.stack.length


(** type for possible moves, here only two choices: pick one or pick
    two numbers on top of the stack. *)
type move = PickOne | PickTwo

(** QUESTION 2 *)

(** set of possible moves in a given configuration. It is either empty,
    represented as `None` or a pair of two moves represented as
    `Some(m1,m2)` *)
let function legal_moves (c:config) : option (move, move) =
  if c.pos >= c.stack.length - 1 then None else Some(PickOne,PickTwo)

(** QUESTION 3 *)

(** `do_move c m` returns the new configuration obtained from
    configuration `c` after playing move `m` *)
let function do_move (c:config) (m:move) : config
  requires { inv c }
  requires { legal_moves c <> None }
  ensures { inv result }
=
  match m, c.turn with
  | PickOne, Alice ->
    let x = c.stack[c.pos] in
    { c with turn = Bob; pos = c.pos + 1; score_alice = c.score_alice + x }
  | PickOne, Bob ->
    let x = c.stack[c.pos] in
    { c with turn = Alice; pos = c.pos + 1; score_bob = c.score_bob + x }
  | PickTwo, Alice ->
    let x1 = c.stack[c.pos] in
    let x2 = c.stack[c.pos+1] in
    { c with turn = Bob; pos = c.pos + 2; score_alice = c.score_alice + x1 + x2 }
  | PickTwo,Bob ->
    let x1 = c.stack[c.pos] in
    let x2 = c.stack[c.pos+1] in
    { c with turn = Alice; pos = c.pos + 2; score_bob = c.score_bob + x1 + x2 }
  end

(** `config_value c` is an heuristic evaluation of the config `c`,
     Alice wants to minimize it whereas Bob wants to maximize it *)
let function config_value (c:config) : int =
   c.score_alice - c.score_bob

use array.Init

(* QUESTION 4 *)
let config0 () : config
  ensures { result.stack.length = 5 }
  ensures { result.stack[0] = 20 }
  ensures { result.stack[1] = 17 }
  ensures { result.stack[2] = 24 }
  ensures { result.stack[3] = -30 }
  ensures { result.stack[4] = 27 }
  ensures { result.pos = 0 }
  ensures { result.turn = Alice }
  ensures { result.score_alice = 0 }
  ensures { result.score_bob = 0 }
  ensures { inv result }
  =
   let s = init 5 [| 20; 17; 24; -30; 27 |] in
   { stack = s;
     pos = 0;
     turn = Alice;
     score_alice = 0;
     score_bob = 0;
   }

(* You can run the test above using:
    why3 execute min_game.mlw --use MinGame "config0 ()"
*)


end




(** {2 The MiniMax value of a tree of moves} *)

module MiniMax

  use MinGame
  use int.MinMax

(* QUESTION 5 *)

  (** [minimax c d] returns the minimax evaluation of config [p]
      at depth [d].  *)
  let rec function minimax (c:config) (d:int) : int
    requires { inv c }
    variant { d }
 = if d <= 0 then config_value c else
    match legal_moves c with
    | None -> config_value c
    | Some(m1,m2) ->
      match c.turn with
      | Alice -> (* Alice wants to minimize the config value *)
        min (minimax (do_move c m1) (d-1)) (minimax (do_move c m2) (d-1))
      | Bob -> (* Bob wants to maximize the config value *)
        max (minimax (do_move c m1) (d-1)) (minimax (do_move c m2) (d-1))
      end
    end

(* QUESTION 6 *)

(** Testing `minimax` on the sample configuration by concrete execution *)
let test0 () =
  let c0 = config0 () in
  (minimax c0 0,
   minimax c0 1,
   minimax c0 2,
   minimax c0 3,
   minimax c0 4,
   minimax c0 5)

(* You can run the tests above using:
    why3 execute min_game.mlw --use MiniMax "test0 ()"
*)

(** Testing `minimax` on the sample configuration with provers *)
let test_minimax () =
  let c0 = config0 () in
  assert { minimax c0 0 = 0 };
  assert { minimax c0 1 = 20 };
  assert { minimax c0 2 = 3 };
  assert { minimax c0 3 = -3 }

end





(** {2 The alpha-beta algorithm} *)

module AlphaBeta

  use int.MinMax
  use MinGame
  use MiniMax

  (** `lt_alpha_n alpha n` means `alpha < n` *)
  let predicate lt_alpha_n (alpha: option int) (n:int) =
    match alpha with
    | None -> true
    | Some a -> a < n
    end

  (** `lt_n_beta n beta` means `n < beta` *)
  let predicate lt_n_beta (n:int) (beta: option int) =
    match beta with
    | None -> true
    | Some b -> n < b
    end
    
  (** `lt_alpha_beta alpha beta` means `alpha < beta` *)
  let predicate lt_alpha_beta (alpha: option int) (beta: option int) =
    match alpha, beta with
    | Some a, Some b -> a < b
    | _ -> true
    end

  (** `max_alpha alpha n` is the maximum of `alpha` and `n` *)
  let max_alpha (alpha: option int) (n:int) : option int =
    match alpha with
    | None -> Some n
    | Some a -> Some (max a n)
    end

  (** `min_beta n beta` is the minimum of `n` and `beta` *)
  let min_beta (n:int) (beta: option int) : option int =
    match beta with
    | None -> Some n
    | Some b -> Some (min n b)
    end

(* QUESTIONS 7 and 8 *)

  let rec alpha_beta (alpha beta:option int) (c:config) (d:int) : int
    requires { inv c }
    requires { lt_alpha_beta alpha beta }
    variant  { d }
    ensures  { lt_alpha_n alpha (minimax c d) <-> lt_alpha_n alpha result }
    ensures  { lt_n_beta (minimax c d) beta <-> lt_n_beta result beta }
    ensures  { lt_alpha_n alpha (minimax c d) -> lt_n_beta (minimax c d) beta -> result = minimax c d }
  = if d <= 0 then config_value c else
    match legal_moves c with
    | None -> config_value c
    | Some(m1,m2) ->
      match c.turn with
      | Alice ->
        let v1 = alpha_beta alpha beta (do_move c m1) (d-1) in
        if lt_alpha_n alpha v1 then
          min v1 (alpha_beta alpha (min_beta v1 beta) (do_move c m2) (d-1))
        else
          v1
      | Bob ->
        let v1 = alpha_beta alpha beta (do_move c m1) (d-1) in
        if lt_n_beta v1 beta then
          max v1 (alpha_beta (max_alpha alpha v1) beta (do_move c m2) (d-1))
        else
          v1
      end
    end

  (* QUESTIONS 9 and 10 *)

  exception GameEnded

  let best_move (c:config) (d:int) : move
    requires { inv c }
    requires { d >= 1 }
    ensures { minimax c d = minimax (do_move c result) (d-1) }
    raises { GameEnded -> legal_moves c = None }
  =
    match legal_moves c with
    | None -> raise GameEnded
    | Some(m1,m2) ->
      let v = minimax (do_move c m1) (d-1) in
      match c.turn with
      | Alice ->
        if v < alpha_beta (Some v) (Some (v+1)) (do_move c m2) (d-1) then
          m1
        else
          m2
      | Bob ->
        if alpha_beta (Some (v-1)) (Some v) (do_move c m2) (d-1) < v then
          m1
        else
          m2
      end
    end

  let test_alphabeta () =
    let c0 = config0 () in
    try
    (best_move c0 1,
     best_move c0 2,
     best_move c0 3,
     best_move c0 4)
     with GameEnded -> absurd
     end

  (* You can run the test above using:
      why3 execute min_game.mlw --use AlphaBeta "test_alphabeta ()"
  *)

end






(** {2 An efficient algorithm for the Min game} *)

(** Best move found by dynamic programming *)



module Dynamic



use int.Int
use int.MinMax
use array.Array

use MinGame
use MiniMax

(** QUESTION 13 *)

(* Lemma relating the minimax value of a configuration to the minimax
value of the same configuration with score set to zero.  *)

  let rec lemma minimax_independence (c:config) (d:int) : unit
    requires { inv c }
    variant { d }
    ensures { minimax c d =
              minimax { c with score_alice = 0 ; score_bob = 0 } d +
              config_value c }
  =
    if d > 0 then match legal_moves c with
                  | None -> ()
                  | Some (m1,m2) ->
                            minimax_independence (do_move c m1) (d-1);
                            minimax_independence (do_move c m2) (d-1);
                            minimax_independence (do_move { c with score_alice = 0 ; score_bob = 0 } m1) (d-1);
                            minimax_independence (do_move { c with score_alice = 0 ; score_bob = 0 } m2) (d-1)
                  end


(* QUESTION 16 *)

predicate dynamic_inv_at_index  (stack:array int) (i:int) (a:array int) =
  forall t:player, sa sb : int, d:int. 
    let c =
      { stack = stack; pos = i; turn = t; score_alice = sa; score_bob = sb }
    in
    (* relating `minimax c d` to a[i] *)
    d >= stack.length - i - 1 ->
    minimax c d = match t with
                  | Alice -> sa - sb + a[i]
                  | Bob -> sa - sb - a[i]
                  end

(* QUESTION 15 and 17 *)

let dynamic (stack : array int) : int
  requires { stack.length >= 1 }
  ensures { forall d:int. d >= stack.length - 1 -> minimax (initial_config stack) d = result }
	    (* relating the result to minimax *)
=
  let l = stack.length in
  let a = Array.make (l+1) 0 in
  for n = l-2 downto 0 do
    invariant { dynamic_inv_at_index stack (n+1) a /\ dynamic_inv_at_index stack (n+2) a }
    a[n] <- min (stack[n] - a[n+1]) (stack[n] + stack[n+1] - a[n+2])
  done;
  a[0]

  let test () =
    let c0 = config0 () in
    dynamic c0.stack
  (* You can run the test above using:
      why3 execute min_game.mlw --use Dynamic "test ()"
   *)



(* QUESTION 18 *)

predicate dynamic_inv  (stack:array int) (i:int) (a:int) =
  forall t:player, sa sb : int, d:int. 
    let c =
      { stack = stack; pos = i; turn = t; score_alice = sa; score_bob = sb }
    in
    (* relating `minimax c d` to a *)
    d >= stack.length - i - 1 ->
    minimax c d = match t with
                  | Alice -> sa - sb + a
                  | Bob -> sa - sb - a
                  end


let dynamic_opt (stack : array int) : int
  requires { stack.length >= 1 }
  ensures { forall d:int. d >= stack.length - 1 -> minimax (initial_config stack) d = result }
= 
  let l = stack.length in
  let ref a = 0 in
  let ref b = 0 in
  for n = l-2 downto 0 do
    invariant { dynamic_inv stack (n+1) a /\ dynamic_inv stack (n+2) b }
    let temp = a in
    a <- min (stack[n] - a) (stack[n] + stack[n+1] - b);
    b <- temp
  done;
  a


end

